# Maintainer: ParchLinux

pkgname=parch-welcome
pkgver=2.0.0
pkgrel=1
pkgdesc="Welcome application for Parch Linux"
arch=('x86_64')
url="https://github.com/parchlinux/parch-welcome"
license=('GPL-3')
depends=('gcc-libs')
makedepends=('git' 'rust')
source=("$pkgname::git+$url#branch=main")
md5sums=('SKIP')

build() {
  cd "$srcdir/$pkgname"
  cargo build --release
}

package() {
  cd "$srcdir/$pkgname"

  # copy license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # copy logo
  install -Dm644 "${srcdir}/${pkgname}/resources/logo.png" "${pkgdir}/usr/share/icons/${pkgname}.png"

  # copy autostart desktop file
  install -Dm644 "${srcdir}/${pkgname}/resources/autostart.desktop" "${pkgdir}/etc/xdg/autostart/${pkgname}.desktop"

  # copy application desktop file
  install -Dm644 "${srcdir}/${pkgname}/resources/application.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # install binary
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
